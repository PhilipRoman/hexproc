#include "classfile.hxp"

// class file magic bytes
ca fe ba be

VERSION(8, 0)

CONSTANT_POOL
	CLASS(JvmHello,    "JvmHello")
	CLASS(Object,      "java/lang/Object")
	CLASS(PrintStream, "java/io/PrintStream")
	CLASS(System,      "java/lang/System")

	STRING(message, "Hello, world!")

	FIELD(System.out, System, "out", "Ljava/io/PrintStream;")
	METHOD(PrintStream.println, PrintStream, "println", "(Ljava/lang/String;)V")

	UTF8(main.name, "main")
	UTF8(main.type, "([Ljava/lang/String;)V")
	UTF8(codeAttributeName, "Code")

[2](public | final) // class access flags
[2]JvmHello [2]Object // this class, super class

TABLE_LENGTH(0) // interfaces

TABLE_LENGTH(0) // fields

TABLE_LENGTH(1) // methods
	// main method header
	[2](public | static)
	[2]main.name [2]main.type // name, type
	[2](1) // number of attributes
	[2]codeAttributeName // 'Code'
		[4](main.codeAttribute.end - main.codeAttribute) // 'Code' attribute size
		main.codeAttribute:
		[2](2) [2](1) // max stack size, max local size
		[4](main.code.end - main.code) // code size
		main.code:
			getstatic(System.out)
			ldc(message)
			invokevirtual(PrintStream.println)
			return_void
		main.code.end:
		[2](0) // exception table size
		[2](0) // number of attributes for this attribute
		main.codeAttribute.end:

TABLE_LENGTH(0) // attributes
